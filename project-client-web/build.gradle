apply plugin: "gwt"
apply plugin: "war"
apply plugin: 'java'
apply plugin: "jetty"

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {

    compile project(":project-client")

    compile "com.badlogicgames.gdx:gdx:$GDX_VERSION"
    compile "com.badlogicgames.gdx:gdx:$GDX_VERSION:sources"
    compile "com.badlogicgames.gdx:gdx-backend-gwt:$GDX_VERSION"
    compile "com.badlogicgames.gdx:gdx-backend-gwt:$GDX_VERSION:sources"

}

gwt {
    gwtVersion = '2.6.1'

    modules 'com.project.client.Web'
    devModules 'com.project.client.WebDev'

    maxHeapSize = "1024M"

    src += files(project(':project').sourceSets.main.allJava.srcDirs)
    src += files(project(':project-client').sourceSets.main.allJava.srcDirs)

    compiler {
        strict = true;
        disableCastChecking = true;
        enableClosureCompiler = true;
    }
}

draftWar {
    from "war"
}

task draftRun(type: JettyRunWar, dependsOn: draftWar) {
    dependsOn.remove('war')
    webApp = draftWar.archivePath
    daemon = true
}

task superDev(type: de.richsource.gradle.plugins.gwt.GwtSuperDev, dependsOn: draftRun)

task dist(dependsOn: [clean, compileGwt]) {
    doLast {
        file("build/dist").mkdirs()
        copy {
            from "build/gwt/out"
            into "build/dist"
            exclude "WEB-INF"
        }
        copy {
            from "src/main/webapp"
            into "build/dist"
            exclude "WEB-INF"
        }
        copy {
            from "war"
            into "build/dist"
            exclude "WEB-INF"
        }
    }
}